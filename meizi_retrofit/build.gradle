apply plugin: 'com.android.application'
android {
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "info.meizi_retrofit"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 6
        versionName "1.0.6"
        manifestPlaceholders = [CHANNEL_VALUE: "xiaomi"]
    }
    productFlavors {
        meizu {}
        wandoujia {}
        xiaomi {}
        baidu {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 输出apk名称为Meizi_v1.0_wandoujia.apk
                def fileName = "Meizi_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
    signingConfigs {
        debug {

        }
        release {
            keyAlias 'key'
            keyPassword 'keyPassword'
            storeFile file('storeFile')
            storePassword 'storePassword'
        }
        release1 {
            File propFile = file('sign.properties');
            if (propFile.exists()) {
                println("文件存在")
                def Properties props = new Properties()
                props.load(new FileInputStream(propFile))
                if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
                    storeFile file(props['STORE_FILE'])
                    storePassword props['STORE_PASSWORD']
                    keyAlias props['KEY_ALIAS']
                    keyPassword props['KEY_PASSWORD']
                }
            } else {
                println("文件不存在")
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "TEST_CONFIG", "false"

            debuggable false
            minifyEnabled true//混淆 还有点问题
            zipAlignEnabled true//优化
            shrinkResources true//去除无用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'//混淆文件
            signingConfig signingConfigs.release1//使用key这个闭包进行签名

        }
    }
    packagingOptions {

        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:recyclerview-v7:23.0.+'
    compile 'com.android.support:cardview-v7:23.0.+'
    compile 'com.android.support:design:23.0.+'
    compile 'com.android.support:palette-v7:23.0.+'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.realm:realm-android:0.86.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxjava-math:1.0.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'org.jsoup:jsoup:1.8.2'
    compile 'me.drakeet.materialdialog:library:1.2.2'
    compile files('libs/butterknife-7.0.1.jar')
    //    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    //    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'
    compile files('libs/umeng-update-v2.6.0.1.jar')
}
